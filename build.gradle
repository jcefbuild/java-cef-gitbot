plugins {
   id 'scala'
   id 'application'
}

group 'com.smac89'
version '1.0'

ext {
   scala_version = '2.13'
}

repositories {
   mavenCentral()
}

dependencies {
   implementation "org.scala-lang:scala-library:$scala_version.+"
   implementation "com.github.xuwei-k:ghscala_$scala_version:0.6.0"
   implementation "com.github.xuwei-k:httpz-scalaj_$scala_version:0.6.1"
   implementation "io.lemonlabs:scala-uri_$scala_version:1.5.1"
}

application {
   mainClassName 'com.github.smac89.gitbot.Main'
}

wrapper {
   distributionType = Wrapper.DistributionType.BIN
   gradleVersion = '5.6.4'
}

task stage(dependsOn: ['clean']) {
   finalizedBy('build')
}

startScripts {
   dependsOn jar

   doFirst {
      unixStartScriptGenerator = configure(new CustomUnixStartScript()) {
         classpath = configurations.runtimeClasspath + jar.outputs.files
      }
   }
}

class CustomUnixStartScript implements ScriptGenerator {
   @InputFiles
   FileCollection classpath

   @Override
   void generateScript (JavaAppStartScriptGenerationDetails details, Writer destination) {
      destination.withWriter {
         it << """
            #!/bin/sh
            java -classpath $classpath.asPath ${details.mainClassName}
         """.stripIndent().stripLeading()
      }
   }
}
